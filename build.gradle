apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'org.sonarqube'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'


buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:3.2.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.61"
        classpath 'org.jacoco:org.jacoco.core:0.8.3'
        classpath "org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
    }
}

Properties versionProps = new Properties()
def versionPropsFile = file('version.properties')

if (versionPropsFile.exists())
    versionProps.load(new FileInputStream(versionPropsFile))

def increment = 0

def runTasks = gradle.startParameter.taskNames

if ('build' in runTasks) {
    increment = 1
}

def majorVersion = 3
def minorVersion = 0
def patch = versionProps['VERSION_PATCH'].toInteger() + increment

def verString = majorVersion + '.' + minorVersion + '.' + patch

task printVersion {
    println verString
}

versionProps['VERSION_PATCH'] = patch.toString()
versionProps.store(versionPropsFile.newWriter(), null)  

jacocoTestReport {
    reports {
        xml.enabled=true
        html.enabled=false
    }
}


dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.61"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.0"
    api 'org.bouncycastle:bcpkix-jdk15on:1.64'
    testImplementation "junit:junit:4.12"
    implementation "org.json:json:20160212"
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
    maven {
        url  "https://dl.bintray.com/xyoraclenetwork/xyo"
    }
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/libs/sdk-core-kotlin.jar")
            groupId 'network.xyo'
            artifactId 'sdk-core-kotlin'
            version verString

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')
                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    dryRun = false
    override = false
    publish = true
    pkg {
        repo = 'xyo'
        name = 'sdk-core-kotlin'
        userOrg = 'xyoraclenetwork'
        licenses = ['LGPL-3.0']
        desc = 'Kotlin Core Library'
        vcsUrl = 'https://github.com/xyoraclenetwork/sdk-core-kotlin.git'

        version {
            name = verString
            vcsTag = verString
        }
    }
    publications = ['Production']
}